/*
 * Mantou Earth - Live your wallpaper with live earth
 * Copyright (C) 2015  XiNGRZ <xxx@oxo.ooo>
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git tag'.execute([], project.rootDir).text.trim().split("\n").last()
def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.1'

    defaultConfig {
        applicationId "ooo.oxo.apps.earth"

        minSdkVersion 16

        targetSdkVersion 25

        versionCode 20000 + gitCommits
        versionName gitTag

        manifestPlaceholders = [APP_NAME: '@string/app_name']

        resValue "string", "account_type", "${applicationId}.account"
        resValue "string", "earths_authority", "${applicationId}.earths"

        // Versioning
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        println "Building ${gitTag} (rev. ${gitCommits} on ${gitSha})"

        buildConfigField "String", "SERVER_OXO", project.hasProperty('SERVER_OXO') ? "\"${SERVER_OXO}\"" : "null"
        buildConfigField "String", "SERVER_NICT", "\"%s\""

        buildConfigField "boolean", "USE_OXO_SERVER", "${project.hasProperty('SERVER_OXO')}"
    }

    lintOptions {
        disable "MissingTranslation"
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    if (project.hasProperty('STOREFILE')) {
        signingConfigs {
            release {
                storeFile file(STOREFILE)
                storePassword STOREPASS
                keyAlias KEYALIAS
                keyPassword KEYPASS
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"

            manifestPlaceholders = [APP_NAME: '@string/app_name_debug']

            resValue "string", "account_type", "${defaultConfig.applicationId}.debug.account"
            resValue "string", "earths_authority", "${defaultConfig.applicationId}.debug.earths"
        }

        release {
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (project.hasProperty('STOREFILE')) {
                println "Using release keystore ${signingConfigs.release.storeFile}"
                signingConfig signingConfigs.release
            } else {
                println "Using default key"
            }
        }
    }

    productFlavors {
        github {
            manifestPlaceholders = [UMENG_CHANNEL: "GitHub"]
        }

        google {
            manifestPlaceholders = [UMENG_CHANNEL: "Google"]
        }

        wandoujia {
            manifestPlaceholders = [UMENG_CHANNEL: "Wandoujia"]
        }

        coolapk {
            manifestPlaceholders = [UMENG_CHANNEL: "CookApk"]
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                output.outputFile = file("${output.outputFile.parent}/earth" +
                        "-${variant.buildType.name.toLowerCase()}" +
                        "-${variant.versionName}" +
                        "-${variant.flavorName.toLowerCase()}" +
                        ".apk")
            }
        }
    }
}

dependencies {
    wearApp project(':wear')

    //noinspection GradleDependency
    compile 'com.google.android.gms:play-services-wearable:7.8.87'

    compile 'com.android.support:design:25.0.1'

    compile 'com.squareup.okhttp:okhttp:2.7.5'

    compile 'com.github.bumptech.glide:glide:3.6.1'

    compile 'commons-io:commons-io:2.4'

    compile 'ooo.oxo:data-binding-support:1.1.1'

    compile 'com.umeng.analytics:analytics:latest.integration'
}
